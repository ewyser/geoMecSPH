#----------------------------------------------------------------------------------------------------------
#----------------------------------------------------------------------------------------------------------
function whichType(xn::Float64,xB::Vector{Float64},Δx::Float64)
    if xn==xB[1] ||  xn==xB[2] 
        type = 1::Int64
    elseif xn<(xB[1]+1.1*Δx) && xn>(xB[1]+0.9*Δx) 
        type = 2::Int64
    elseif xn>(xB[1]+1.5*Δx) && xn<(xB[2]-1.5*Δx) 
        type = 3::Int64
    elseif xn<(xB[2]-0.9*Δx) && xn>(xB[2]-1.1*Δx)
        type = 4::Int64
    else
        type = 0::Int64
    end
    return(type::Int64)
end
#----------------------------------------------------------------------------------------------------------
#----------------------------------------------------------------------------------------------------------
function ϕ∇ϕ(ξ::Float64,type::Int64,Δx::Float64)
    ϕ = 0.0
    ∂ϕ= 0.0
    if type==1 
        if -2<=ξ && ξ<=-1 
            ϕ = 1/6     *ξ^3+     ξ^2   +2*ξ    +4/3
            ∂ϕ= 3/(6*Δx)*ξ^2+2/Δx*ξ     +2/Δx
        elseif -1<=ξ && ξ<=-0 
            ϕ = -1/6     *ξ^3           +  ξ    +1
            ∂ϕ= -3/(6*Δx)*ξ^2           +  1/Δx
        elseif  0<=ξ && ξ<= 1 
            ϕ =  1/6     *ξ^3           -  ξ    +1
            ∂ϕ=  3/(6*Δx)*ξ^2           -  1/Δx
        elseif  1<=ξ && ξ<= 2 
            ϕ = -1/6     *ξ^3+     ξ^2  -2*ξ    +4/3
            ∂ϕ= -3/(6*Δx)*ξ^2+2/Δx*ξ    -2/Δx
        end    
    elseif type==2 
        if -1<=ξ && ξ<=0 
            ϕ = -1/3 *ξ^3-     ξ^2    +2/3
            ∂ϕ= -1/Δx*ξ^2-2/Δx*ξ
        elseif 0<=ξ && ξ<=1 
            ϕ =  1/2     *ξ^3-     ξ^2    +2/3
            ∂ϕ=  3/(2*Δx)*ξ^2-2/Δx*ξ
        elseif 1<=ξ && ξ<=2 
            ϕ = -1/6     *ξ^3+     ξ^2-2*ξ+4/3
            ∂ϕ= -3/(6*Δx)*ξ^2+2/Δx*ξ  -2/Δx
        end
    elseif type==3 
        if -2<=ξ && ξ <=-1 
            ϕ =  1/6     *ξ^3+     ξ^2+2*ξ+4/3
            ∂ϕ=  3/(6*Δx)*ξ^2+2/Δx*ξ  +2/Δx
        elseif -1<=ξ && ξ<=0 
            ϕ = -1/2     *ξ^3-     ξ^2    +2/3
            ∂ϕ= -3/(2*Δx)*ξ^2-2/Δx*ξ
        elseif  0<=ξ && ξ<=1
            ϕ =  1/2     *ξ^3-     ξ^2    +2/3
            ∂ϕ=  3/(2*Δx)*ξ^2-2/Δx*ξ
        elseif  1<=ξ && ξ<=2    
            ϕ = -1/6     *ξ^3+     ξ^2-2*ξ+4/3
            ∂ϕ= -3/(6*Δx)*ξ^2+2/Δx*ξ  -2/Δx
        end
    elseif type==4
        if -2<=ξ && ξ<=-1
            ϕ =  1/6     *ξ^3+     ξ^2+2*ξ+4/3
            ∂ϕ=  3/(6*Δx)*ξ^2+2/Δx*ξ  +2/Δx 
        elseif -1<=ξ && ξ<=0
            ϕ = -1/2     *ξ^3-     ξ^2    +2/3
            ∂ϕ= -3/(2*Δx)*ξ^2-2/Δx*ξ      
        elseif 0<=ξ && ξ<=1
            ϕ =  1/3     *ξ^3-     ξ^2    +2/3
            ∂ϕ=  3/(3*Δx)*ξ^2-2/Δx*ξ      
        end
    else
        ϕ = 0.0
        ∂ϕ= 0.0
    end    
    return(ϕ,∂ϕ)
end
#----------------------------------------------------------------------------------------------------------
#----------------------------------------------------------------------------------------------------------
@views function ϕ∂ϕ!(B,ϕ,∂ϕx,∂ϕz,xp,xn,zn,p2n,h,xB,nn,nmp)
    #preprocessing
    xb = copy(xB[1:2])
    zb = copy(xB[3:4])
    Δx = h[1]
    Δz = h[2]
    #action
    @threads for mp in 1:nmp
        for nn in 1:nn
            # compute basis functions
            id     = p2n[mp,nn]
            ξ      = (xp[mp,1] - xn[id])/Δx 
            type   = whichType(xn[id],xb,Δx)
            ϕx,dϕx = ϕ∇ϕ(ξ,type,Δx)
            η      = (xp[mp,2] - zn[id])/Δz
            type   = whichType(zn[id],zb,Δz)
            ϕz,dϕz = ϕ∇ϕ(η,type,Δz)
            # convolution of basis function
            ϕ[mp,nn]   =  ϕx*  ϕz                                        
            ∂ϕx[mp,nn] = dϕx*  ϕz                                        
            ∂ϕz[mp,nn] =  ϕx* dϕz
        end
        # B-matrix assembly
        B[1,1:2:end,mp].= ∂ϕx[mp,:]
        B[2,2:2:end,mp].= ∂ϕz[mp,:]
        B[4,1:2:end,mp].= ∂ϕz[mp,:]
        B[4,2:2:end,mp].= ∂ϕx[mp,:]
    end
end
#----------------------------------------------------------------------------------------------------------
#----------------------------------------------------------------------------------------------------------